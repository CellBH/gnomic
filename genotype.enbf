@@whitespace :: /[\t ]+/
#include :: "sequence-variant.enbf"

start = (@+:change {(sep | list_separator [sep]) @+:change}* | {}) [sep] $;

change = insertion | replacement | deletion | PLASMID | PHENE;

insertion = "+" new:INSERTABLE [markers:MARKERS];

replacement =
     old:REPLACEABLE op:">" new:SUBSTITUTE [markers:MARKERS] |
     old:REPLACEABLE op:">>" new:SUBSTITUTE [markers:MARKERS];

deletion
    = "-" old:DELETABLE [markers:MARKERS];

INSERTABLE
    = FUSION
    | @:FEATURE_SET
    | FEATURE;

REPLACEABLE
    = FUSION
    | @:FEATURE_SET
    | FEATURE;

SUBSTITUTE
    = PLASMID
    | FUSION
    | @:FEATURE_SET
    | FEATURE;

DELETABLE
    = PLASMID
    | FUSION
    | @:FEATURE_SET
    | FEATURE;

PLASMID
    = name:IDENTIFIER contents:FEATURE_SET [markers:MARKERS]
    | name:IDENTIFIER "{}" [markers:MARKERS]
    | "(" name:IDENTIFIER contents:FEATURE_SET_ITEMS ")" [markers:MARKERS]
    | "(" name:IDENTIFIER ")" [markers:MARKERS];

PHENE
    = [organism:FEATURE_ORGANISM] name:IDENTIFIER accession:ACCESSION variant:BINARY_VARIANT
    | accession:ACCESSION variant:BINARY_VARIANT
    | [organism:FEATURE_ORGANISM] name:IDENTIFIER variant:VARIANT;

PHENE_SET
    = "{" [sep] @+:PHENE {(sep | list_separator [sep]) @+:PHENE}* [sep] "}";

MARKERS = "::" (@+:PHENE | @:PHENE_SET);

FEATURE
    = [organism:FEATURE_ORGANISM] [type:IDENTIFIER "."] name:IDENTIFIER [variant:VARIANT] [accession:ACCESSION] [range:RANGE]
    | accession:ACCESSION [range:RANGE];

FEATURE_ORGANISM = @:ORGANISM "/";

FEATURE_SET
    = "{" @:FEATURE_SET_ITEMS "}";

FEATURE_SET_ITEMS
    = [sep] @+:(FEATURE_FUSION | FEATURE) {(sep | list_separator [sep]) @+:(FEATURE_FUSION | FEATURE)}* [sep];

FUSION
    = @+:(FEATURE_SET | FEATURE) {":" @+:(FEATURE_SET | FEATURE)}+;

FEATURE_FUSION
    = @+:FEATURE {":" @+:FEATURE}+;


VARIANT = "(" @:VARIANT_LIST ")" | @:BINARY_VARIANT;

VARIANT_LIST = @:(SEQUENCE_VARIANT | VARIANT_IDENTIFIER) {("," | ";") [sep] @:(SEQUENCE_VARIANT | VARIANT_IDENTIFIER)}*;

BINARY_VARIANT = "+" | "-";

VARIANT_IDENTIFIER = /[A-Za-z0-9]+([A-Za-z0-9_\-]+[A-Za-z0-9])?/;

RANGE = "[" [level:SEQUENCE_LEVEL_PREFIX] start:INTEGER "_" end:INTEGER "]"
      | "[" [level:SEQUENCE_LEVEL_PREFIX] pos:INTEGER "]";

SEQUENCE_LEVEL_PREFIX = @:("g" | "c" | "p") ".";




(* NOTE ACCESSION with its optional ":" can be ambiguous when it is used within a fusion and the IDENTIFIER is not numeric.
   In these cases, a DATABASE should be specified. *)
ACCESSION
    = "#" db:DATABASE ":" id:(INTEGER | IDENTIFIER)
    | "#" id:(INTEGER | IDENTIFIER);

DATABASE = /[A-Za-z0-9-][A-Za-z0-9]+/;

INTEGER = @:/[0-9]+/;

IDENTIFIER = /[A-Za-z0-9]+([A-Za-z0-9_-]+[A-Za-z0-9])?/;

ORGANISM = /[a-zA-Z0-9]+(\.[a-zA-Z0-9]+)?/;

list_separator = ",";

sep = /[\t ]+/;