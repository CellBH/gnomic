@@whitespace :: /[\t ]+/

start = [@+:change {(sep | list_separator [sep]) @+:change}*] [sep] $;

change = insertion | replacement | deletion | PLASMID | PHENE;

insertion = "+" new:INSERTABLE [marker:MARKER];

replacement =
     old:FEATURE ">" new:INSERTABLE [marker:MARKER] |
     old:FEATURE ">>" new:INSERTABLE [marker:MARKER];

deletion
    = "-" old:INSERTABLE [marker:MARKER];

INSERTABLE
    = PLASMID
    | @:FEATURE_SET
    | FUSION
    | FEATURE;

PLASMID
    = name:IDENTIFIER contents:FEATURE_SET [marker:MARKER]
    | name:IDENTIFIER "{}" [marker:MARKER];

MARKER = "::" @:PHENE;

PHENE
    = [organism:FEATURE_ORGANISM] name:IDENTIFIER accession:ACCESSION variant:BINARY_VARIANT
    | accession:ACCESSION variant:BINARY_VARIANT
    | [organism:FEATURE_ORGANISM] name:IDENTIFIER variant:VARIANT;


FEATURE
    = [organism:FEATURE_ORGANISM] [type:IDENTIFIER "."] name:IDENTIFIER [variant:VARIANT] [accession:ACCESSION] [range:RANGE]
    | accession:ACCESSION [range:RANGE];

FEATURE_ORGANISM = @:ORGANISM "/";

FEATURE_SET
    = "{" @+:(FUSION | FEATURE) {(sep | list_separator [sep]) @+:(FUSION | FEATURE)}* "}"
    | "{" @+:(FUSION | FEATURE) "}";

FUSION
    = @+:FEATURE {":" @+:FEATURE}+;

VARIANT = "(" @:VARIANT_DEFINITION ")" | @:BINARY_VARIANT;

VARIANT_DEFINITION = @:IDENTIFIER {("," | ";") @:IDENTIFIER}*;

BINARY_VARIANT = "+" | "-";

RANGE = "[" [type:RANGE_SEQUENCE_TYPE] start:INTEGER "_" end:INTEGER "]"
      | "[" [type:RANGE_SEQUENCE_TYPE] pos:INTEGER "]";

RANGE_SEQUENCE_TYPE = ("c" | "p") ".";

(* NOTE ACCESSION with its optional ":" can be ambiguous when it is used within a fusion and the IDENTIFIER is not numeric.
   In these cases, a DATABASE should be specified. *)
ACCESSION
    = "#" db:DATABASE ":" id:(INTEGER | IDENTIFIER)
    | "#" id:(INTEGER | IDENTIFIER);

DATABASE = /[A-Za-z0-9-][A-Za-z0-9]+/;

INTEGER = @:/[0-9]+/;

IDENTIFIER = /[A-Za-z0-9]+([A-Za-z0-9_-]+[A-Za-z0-9])?/;

ORGANISM = /[a-zA-Z0-9]+("."[a-zA-Z0-9]+)?/;

list_separator = ",";

sep = /[\t ]+/;